plugins {
    id 'java'
    id 'java-library'
}

repositories {
    mavenCentral()
}

version "0.1"
def libDir = file( "lib" )
def releaseDir = file( "release" )

dependencies {
    implementation fileTree( dir: libDir, include: '*.jar' )
    implementation 'org.jolie-lang:jolie:1.10.4'
    implementation 'org.jolie-lang:libjolie:1.10.4'
    implementation 'org.jolie-lang:jolie-cli:1.10.4'
}

compileJava   {
  sourceCompatibility = '11'
  targetCompatibility = '11'
}

jar {
    enabled = true

    manifest {
        attributes 'Main-Class': 'ExecCommandService'
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

task makeRelease() {
    tasks.compileJava.mustRunAfter( clean )
    dependsOn( clean )
    dependsOn( build )
    doLast{
        copy {
            from( buildDir.toPath().resolve( "libs" ) ){
                include "execCommand*.jar"
            }
            into releaseDir.toPath().resolve( "dist" )
        }
        copy {
            from( libDir ){
                include "jsdt-core*.jar"
            }
            into releaseDir.toPath().resolve( "dist" )
        }
        copy {
            from( projectDir ){
                include "jpm.json"
                include "main.ol"
                include "README.md"
                filter( ReplaceTokens, tokens: [ VERSION: version ] )
            }
            into releaseDir
        }
    }
}

clean.doFirst{
    delete releaseDir
}